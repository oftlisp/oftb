(module ministd/internal/prelude/list
  [append assoc each filter find first-nonnil first-some flat-map foldl foldr
   index-into length map nth partition reverse skip split-at take take-while]
  no-prelude)

(import ministd/internal/prelude/either
  either)
(import ministd/internal/prelude/function
  flip)
(import ministd/internal/prelude/intrinsics
  + - = car cdr cons equals nil nil?)
(import ministd/internal/prelude/option
  none some some?)
(import ministd/internal/prelude/pair
  map-pair pair)

(intrinsics:defn append (l r)
  (intrinsics:defn helper (l r)
    (if (nil? l)
      r
      (helper (cdr l) (cons (car l) r))))
  (helper (reverse l) r))

(intrinsics:defn assoc (x l)
  (if (nil? l)
    none
    (if (equals (car (car l)) x)
      (some (cdr (car l)))
      (assoc x (cdr l)))))

(intrinsics:defn each (f l)
  (if (nil? l)
    nil
    (progn
      (f (car l))
      (each f (cdr l)))))

(intrinsics:defn filter (pred l)
  (intrinsics:defn helper (l acc)
    (if (nil? l)
      acc
      (helper
        (cdr l)
        (if (pred (car l))
          (cons (car l) acc)
          acc))))
  (reverse (helper l nil)))

(intrinsics:defn find (pred l)
  (if (nil? l)
    none
    (if (pred (car l))
      (some (car l))
      (find pred (cdr l)))))

(intrinsics:defn first-nonnil (l)
  (if (nil? l)
    nil
    (if (nil? (car l))
      (first-nonnil (cdr l))
      (car l))))

(intrinsics:defn first-some (l)
  (if (nil? l)
    none
    (if (some? (car l))
      (car l)
      (first-some (cdr l)))))

(intrinsics:defn flat-map (f l)
  (intrinsics:defn helper (l acc)
    (if (nil? l)
      acc
      (helper (cdr l) (append (reverse (f (car l))) acc))))
  (reverse (helper l nil)))

(intrinsics:defn foldl (f x l)
  (if (nil? l)
    x
    (foldl f (f x (car l)) (cdr l))))

(intrinsics:defn foldr (f x l)
  (if (nil? l)
    x
    (f (car l) (foldr f x (cdr l)))))

(intrinsics:defn index-into (indices l)
  (foldl (flip nth) l indices))

(intrinsics:defn length (l)
  (foldl (fn (l _) (+ l 1)) 0 l))

(intrinsics:defn map (f l)
  (reverse (foldl (fn (a x) (cons (f x) a)) '() l)))

(intrinsics:defn nth (n l)
  (car (skip n l)))

(intrinsics:defn partition (es)
  (intrinsics:defn helper (es ls rs)
    (if (nil? es)
      (pair ls rs)
      (either
        \(helper (cdr es) (cons $ ls) rs)
        \(helper (cdr es) ls (cons $ rs))
        (car es))))
  (map-pair reverse reverse (helper es nil nil)))

(intrinsics:defn reverse (l)
  (foldl (fn (a x) (cons x a)) '() l))

(intrinsics:defn skip (n l)
  (if (= n 0)
    l
    (skip (- n 1) (cdr l))))

(intrinsics:defn split-at (n l)
  (pair (take n l) (skip n l)))

(intrinsics:defn take (n l)
  (intrinsics:defn helper (n l acc)
    (if (= n 0)
      acc
      (helper (- n 1) (cdr l) (cons (car l) acc))))
  (reverse (helper n l nil)))

(intrinsics:defn take-while (pred l)
  (intrinsics:defn helper (l acc)
    (if (nil? l)
      acc
      (if (pred (car l))
        (helper (cdr l) (cons (car l) acc))
        acc)))
  (reverse (helper l nil)))
