(module macro-expander/modules
  [expand-modules find-decls module-name namespaced-name prelude-name])

(import macro-expander/expand
  [expand-decls])
(import ministd/data/tree-set
  [list->set set-has?])

(intrinsics:defn expand-modules (std-name mods-srcs)
  (intrinsics:def mods (expand-module (prelude-name std-name) mods-srcs std-name nil))
  (map \(pair (nth 0 $) (nth 1 $))
    (expand-module 'main mods-srcs std-name mods)))

; Expands all the declarations in a module, returning a list of expanded modules.
(intrinsics:defn expand-module (target-module mod-srcs std-name mods)
  ; The source of the target module.
  (intrinsics:def mod-src (expect
    (find \(equals (module-name $) target-module) mod-srcs)
    (string-append "Module not found: " (symbol->string target-module))))
  ; All import decls.
  (intrinsics:def mod-attrs (skip 3 (car mod-src)))
  (intrinsics:def imports (if (contains? 'no-prelude mod-attrs)
    (find-imports-no-prelude mod-src)
    (find-imports mod-src std-name mod-srcs)))
  ; A list of modules not yet in the mods assoc.
  (intrinsics:def missing-modules (filter
    \(none? (lookup $ mods))
    (map
      \(nth 1 $)
      imports)))
  (if (nil? missing-modules)
    ; If no modules are missing, build the environment and expand the declarations inside the
    ; module. Then, return the module list with this module added.
    (progn
      (intrinsics:def tmp (build-initial-env imports mods))
      (intrinsics:def env (fst tmp))
      (intrinsics:def macros (snd tmp))
      (intrinsics:def decls (find-decls mod-src))

      (intrinsics:def initial-env-length (length env))
      (intrinsics:def initial-macros-length (length macros))

      (intrinsics:def tmp (expand-decls decls env macros))
      (intrinsics:def decls (nth 0 tmp))
      (intrinsics:def env (nth 1 tmp))
      (intrinsics:def macros (nth 2 tmp))

      (intrinsics:def m (list
        target-module
        (append imports decls)
        (take (- (length env) initial-env-length) env)
        (take (- (length macros) initial-macros-length) macros)))
      (cons m mods))
    ; If there are missing modules, try expanding the first one, then try again with that one
    ; added.
    (progn
      (intrinsics:def new-mods (expand-module (car missing-modules) mod-srcs std-name mods))
      (expand-module target-module mod-srcs std-name new-mods))))

; Builds an initial environment from a list of import decls and an assoc of expanded module
; environments. Returns a pair (INTERP-ENV INTERP-MACROS).
(intrinsics:defn build-initial-env (imports mods)
  (intrinsics:defn helper (l acc)
    (list '> helper l acc)
    (if (nil? l)
      (map-pair reverse reverse acc)
      (progn
        (intrinsics:def value (car l))
        (intrinsics:def name (fst value))
        (intrinsics:def acc (either
          \(map-fst (intrinsics:fn (ds) (cons (pair name $) ds)) acc)
          \(map-snd (intrinsics:fn (ms) (cons (pair name $) ms)) acc)
          (snd value)))
        (helper (cdr l) acc))))
  (helper (flat-map \(process-import $ mods) imports) (pair nil nil)))

; Returns the name of a module.
(intrinsics:defn module-name (m)
  (index-into '(0 1) m))

; Returns the exports of a module as a vector.
(intrinsics:defn module-exports (m)
  (index-into '(0 2) m))

; Returns all the declarations from a module. (Really, just skips the module form and imports.)
(intrinsics:defn find-decls (m)
  (skip-while \(shl? 'import $) (cdr m)))

; Returns all the import forms from a module without injecting the prelude import.
(intrinsics:defn find-imports-no-prelude (m)
  (take-while \(shl? 'import $) (cdr m)))

; Returns all the import forms from a module, injecting the prelude import.
(intrinsics:defn find-imports (m std-name mod-srcs)
  (intrinsics:def prelude (prelude-name std-name))
  (intrinsics:def prelude-exports (module-exports
    (expect
      (find \(eq (module-name $) prelude) mod-srcs)
      "Missing prelude? (Did the prelude try to import itself?)")))
  (cons
    (list 'import prelude prelude-exports)
    (take-while \(shl? 'import $) (cdr m))))

(intrinsics:defn namespaced-name (mod-name sym-name)
  (string->symbol
    (string-append
      (symbol->string mod-name)
      (string-append
        ":"
        (symbol->string sym-name)))))

; Takes an import form and an assoc of already-expanded modules. Returns a list of pairs, where the
; fst is the imported value name and the snd is an either. The left of the either is a value from
; interpreting the module. The right of the either is a function that runs the macro.
(intrinsics:defn process-import (imp mods)
  (intrinsics:def mod-name (nth 1 imp))
  (intrinsics:def plain-imports (vector->list (nth 2 imp)))
  (intrinsics:def attrs (skip 3 imp))

  (intrinsics:def m (assoc mod-name mods))

  (intrinsics:def no-import-decls (list->set (option->nullable
    (lookup 'no-import-decl attrs))))
  (intrinsics:def no-import-macros (list->set (option->nullable
    (lookup 'no-import-macro attrs))))

  (map
    \(progn
      (intrinsics:def f
        (if (set-has? $ no-import-decls)
          (if (set-has? $ no-import-macros)
            (panic (string-concat (list
              "Symbol "
              (symbol->string $)
              " was excluded from both decl and macro imports (from module "
              (symbol->string mod-name)
              ")")))
            process-import-macro)
          (if (set-has? $ no-import-macros)
            process-import-decl
            process-import-either)))
      (expect (f mod-name $ mods)
        (string-concat (list
          "Couldn't import "
          (symbol->string $)
          " from "
          (symbol->string mod-name)))))
    plain-imports))

(intrinsics:defn process-import-decl (m name mods)
  (option-map \(cons name (left $))
    (lookup name (nth 1 (assoc m mods)))))

(intrinsics:defn process-import-either (m name mods)
  (option-cases some (intrinsics:fn () (process-import-macro m name mods))
    (process-import-decl m name mods)))

(intrinsics:defn process-import-macro (m name mods)
  (option-map \(cons name (right $))
    (lookup name (nth 2 (assoc m mods)))))

(intrinsics:defn prelude-name (std-name)
  (string->symbol (string-append (symbol->string std-name) "/prelude")))
