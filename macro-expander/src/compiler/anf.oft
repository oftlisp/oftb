(module macro-expander/compiler/anf
  [module->anf])

(import macro-expander/util
  [global-name])

(intrinsics:defn aexpr? (expr)
  (or (shl? 'fn expr)
  (or (shl? 'get-method expr)
  (or (shl? 'lit expr)
  (or (shl? 'var expr)
      (shl? 'vector expr))))))

(intrinsics:defn anf->aexpr (expr kont)
  (if (aexpr? expr)
    (kont expr)
    (progn
      (intrinsics:def name (gensym))
      (list 'let name expr (kont (list 'var name))))))

(intrinsics:defn anfs->aexprs (exprs kont)
  (intrinsics:defn helper (exprs aexprs)
    (if (nil? exprs)
      (kont (reverse aexprs))
      (anf->aexpr (car exprs)
        (intrinsics:fn (aexpr)
          (helper (cdr exprs) (cons aexpr aexprs))))))
  (helper exprs nil))

(intrinsics:defn call->anf (func args)
  (anf->aexpr (expr->anf func) (intrinsics:fn (func)
    (anfs->aexprs (map expr->anf args) (intrinsics:fn (args)
      (cons 'call (cons func args)))))))

(intrinsics:defn decl->anf (decl)
  (if (shl? 'intrinsics:def decl)
    (append (take 2 decl) (list (expr->anf (nth 2 decl))))
    (if (shl? 'intrinsics:defn decl)
      (append (take 3 decl) (progn->anf (skip 3 decl)))
      (panic (cons 'unknown-decl decl)))))

(intrinsics:defn expr->anf (expr)
  (if (shl? 'intrinsics:def expr)
    (list 'seq
      (nth 2 expr)
      (list 'lit nil))
    (if (shl? 'intrinsics:defn expr)
      (list 'lit nil)
      (if (shl? 'if expr)
        (anf->aexpr (expr->anf (nth 1 expr))
          \(list 'if
            $
            (expr->anf (nth 2 expr))
            (if (= (length expr) 3)
              '(lit ())
              (expr->anf (nth 3 expr)))))
        (if (shl? 'intrinsics:fn expr)
          (list 'fn none (nth 1 expr) (progn->anf (skip 2 expr)))
          (if (shl? 'intrinsics:get-method expr)
            (anf->aexpr (expr->anf (nth 1 expr))
              \(list 'get-method $ (nth 2 expr)))
            (if (shl? 'quote expr)
              (list 'lit (nth 1 expr))
              (if (shl? 'progn expr)
                (progn->anf (cdr expr))
                (if (vector? expr)
                  (anfs->aexprs (map expr->anf (vector->list expr)) \(cons 'vector $))
                  (if (symbol? expr)
                    (list 'var expr)
                    (if (cons? expr)
                      (call->anf (car expr) (cdr expr))
                      (if (or (function? expr) (nil? expr))
                        (panic (cons 'invalid-expr expr))
                        (list 'lit expr)))))))))))))

(intrinsics:defn module->anf (m)
  (intrinsics:def imports (take-while \(shl? 'import $) m))
  (intrinsics:def decls (skip-while \(shl? 'import $) m))
  (pair (flat-map flatten-import imports) (map decl->anf decls)))

(intrinsics:defn progn->anf (exprs)
  (intrinsics:defn helper (exprs anf-expr defns)
    (if (nil? exprs)
      (save-letrec anf-expr defns)
      (progn
        (intrinsics:def expr (car exprs))
        (if (shl? 'intrinsics:def expr)
          (helper (cdr exprs)
            (list 'let (nth 1 expr) (expr->anf (nth 2 expr)) (save-letrec anf-expr defns)) nil)
          (if (shl? 'intrinsics:defn expr)
            (helper (cdr exprs) anf-expr (cons expr defns))
            (helper (cdr exprs) (save-letrec (list 'seq (expr->anf expr) anf-expr) defns) nil))))))
  (intrinsics:defn save-letrec (anf-expr defns)
    (if (nil? defns)
      anf-expr
      (list 'letrec
        (map \(list (nth 1 $) (nth 2 $) (progn->anf (skip 3 $))) defns)
        anf-expr)))

  (intrinsics:def exprs (reverse exprs))
  (if (nil? exprs)
    (panic 'empty-progn)
    (helper (cdr exprs) (expr->anf (car exprs)) nil)))

(intrinsics:defn flatten-import (i)
  (map
    \(pair $ (global-name (nth 1 i) $))
    (vector->list (nth 2 i))))
