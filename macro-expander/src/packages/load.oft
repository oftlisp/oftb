(module macro-expander/packages/load
  [load-package-from])

(import macro-expander/fs
  walk-dir)
(import macro-expander/packages/type
  package)
(import ministd/internal/oftb
  read-file)

(intrinsics:defn load-package-from (path)
  (def metadata-path (string-append path "/package.oftd"))
  (def metadata (read-file metadata-path))

  (def name (car (expect (assoc 'name metadata)
    "Missing package name")))
  (def components (expect (assoc 'components metadata)
    "Missing package components list"))

  (def components (partition (map
    (fn (component)
      (def type (car component))
      (def component (cdr component))
      (if (eq type 'library)
        (left (load-lib-from path component))
        (if (eq type 'binary)
          (right (load-bin-from path component))
          (panic (string-append "Unknown type: " (symbol->string type))))))
    components)))

  (def libs (fst components))
  (if (not (= (length libs) 1))
    (panic "Only one library component is allowed"))
  (package name path (car libs) (snd components)))

(intrinsics:defn load-bin-from (pkg-path component)
  (def name (car (expect
    (assoc 'name component)
    "Binary component missing name")))
  (def path (car (expect
    (assoc 'path component)
    "Binary component missing path")))
  (def path (foldl string-append "" (list
    pkg-path
    "/"
    path)))
  (pair name (read-file path)))

(intrinsics:defn load-lib-from (pkg-path component)
  (map read-file (walk-dir (string-append pkg-path "/src"))))
