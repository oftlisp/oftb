(module main
  [main])

(import macro-expander/fs
  [walk-dir])
(import macro-expander/packages/load
  [load-package-from])
(import ministd/data/tree-set
  [empty-set list->set set->list set-difference set-insert])
(import ministd/internal/oftb
  [read-file])

(intrinsics:defn main (args)
  (if (not (= (length args) 1))
    (panic "usage: make-env ministd-dir"))
  (intrinsics:def intrinsics (search-for-intrinsics (nth 0 args)))

  (println '(module macro-expander/interpreter/env [intrinsics]))
  (println)
  (println (list 'intrinsics:def 'intrinsics (cons 'list (make-env intrinsics)))))

(intrinsics:defn make-env (intrinsics)
  (map
    \(list 'cons (list 'quote $) (list 'cons $ -1))
    (sort (set->list (set-difference
      intrinsics
      (list->set '(intrinsics:def intrinsics:defn intrinsics:fn)))))))

(intrinsics:defn extract-intrinsics (value set)
  (if (symbol? value)
    (if (intrinsic? value)
      (set-insert value set)
      set)
    (if (cons? value)
      (extract-intrinsics (car value) (extract-intrinsics (cdr value) set))
      (if (vector? value)
        (if (= (vector-length value) 0)
          set
          (extract-intrinsics
            (vector-nth 0 value)
            (extract-intrinsics (vector-slice 1 (vector-length value) value) set)))
        set))))

; Returns whether the symbol is a global name from the `intrinsics` package.
(intrinsics:defn intrinsic? (sym)
  (intrinsics:def s (symbol->string sym))
  (if (string-search ":" s)
    (if (> (string-length s) 10)
      (equals (string-slice 0 10 s) "intrinsics")
      false)))

; Converts a name like `string_to_symbol` to `string->symbol`.
(intrinsics:defn name-for-intrinsic (sym)
  (intrinsics:def str (string-replace "_" "-" (string-replace "_to_" "->"
      (symbol->string sym))))
  (if (> (string-length str) 3)
    (if (equals (string-slice 0 3 str) "is-")
      (string-append
        (string-slice 3 (string-length str) str)
        "?")
      str)
    str))

(intrinsics:defn search-for-intrinsics (path)
  (intrinsics:def files (map read-file (walk-dir (string-append path "/src/internal/prelude"))))
  (extract-intrinsics files empty-set))
